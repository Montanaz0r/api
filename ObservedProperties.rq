#+ summary: Retrieves all the information about ILIAD ObservedProperties
#+ description: NOTE! make sure response content type is application/json
#+ endpoint: https://www.foodie-cloud.org/sparql?default-graph-uri=http://w3id.org/iliad/dataaset/jf-pilot-sample
#+ tags:
#+   - json
#+ method: GET
#+ pagination: 100
#+ endpoint_in_url: False
#+ transform: {
#+   "description": "?info",
#+   "$anchor": "description",
#+   "@iot.count": "?total",
#+   "@iot.nextLink": "?next_link",
#+   "value": {
#+     "@iot.id": "?id",
#+     "@iot.selfLink": "?id_link",
#+     "description": "?desc_info",
#+     "name": "?name",
#+     "definition": "?obs_property",
#+     "Datastreams@iot.navigationLink": "?ds_link",
#+     },
#+   }

PREFIX iliad-api: <https://w3id.org/iliad/api>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX sosa:  <http://www.w3.org/ns/sosa/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX wgs84:  <http://www.w3.org/2003/01/geo/wgs84_pos#>
PREFIX rdfs:  <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dwc:  <http://rs.tdwg.org/dwc/terms/>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX oim-jf:  <http://w3id.org/iliad/oim/jellyfish#>

SELECT DISTINCT ?obs_property ?info ?desc_info ?name ?ds_link ?id_link ?next_link ?total ?id
WHERE {
    ?s a sosa:ObservationCollection ;
        sosa:observedProperty ?obs_property .
    BIND ("OGC SensorThings API - ObservedProperties" AS ?info)
    BIND (STRAFTER(STR(?obs_property), 'property/') AS ?prop_name_1)
    BIND (STRBEFORE(STR(?prop_name_1), 'Property') AS ?prop_name)
    BIND (STR(CONCAT("The total count for ", ?prop_name)) AS ?desc_info)
    BIND (REPLACE(STR(?obs_property), "^.*/([^/]*)$", "$1") as ?id)
    BIND (STR(CONCAT("ObservedProperty - ", ?id)) AS ?name)
    BIND (URI(CONCAT('https://w3id.org/iliad/api/v1.0/ObservedProperties(', ?id,')')) AS ?id_link)
    BIND (URI(CONCAT('https://w3id.org/iliad/api/v1.0/ObservedProperties(', ?id,')/Datastreams')) AS ?ds_link)
        {
       SELECT (count(?y) as ?total) WHERE 
       { { SELECT DISTINCT ?y WHERE { ?x sosa:observedProperty ?y } } }

    }
    OPTIONAL { ?s ?p ?_page_integer } 
    BIND (URI(CONCAT('https://w3id.org/iliad/api/v1.0/ObservedProperties?$top=100&$skip=', (?_page_integer+1)*100)) AS ?next_link)
}
ORDER BY ?id