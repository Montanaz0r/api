#+ summary: "Retrieve a specific Observation"
#+ description: "NOTE: make sure response content type is application/json"
#+ endpoint: "https://www.foodie-cloud.org/sparql?default-graph-uri=http://w3id.org/iliad/dataaset/jf-pilot-sample"
#+ tags: [ "json" ]
#+ method: "GET"
#+ pagination: 100
#+ transform: {
#+   "@iot.id": "?s",
#+   "$anchor": "@iot.id",
#+   "@iot.selfLink": "?id_link",
#+   "phenomenonTime": "?phenomenonTime",
#+   "resultTime": "?resultTime",
#+   "result": {
#+      "densityOfJF": "?densityOfJF",
#+      "sampleSizeValue": "?sampleSizeValue",
#+      "speciesScientificName": "?scientificName",
#+      "wormsConcept": "?wormsConcept",
#+   },
#+   "Datastream@iot.navigationLink": "?datastream_link",
#+   "FeatureOfInterest@iot.navigationLink": "?feature_link",
#+ }

PREFIX iliad-api: <https://w3id.org/iliad/api>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX sosa:  <http://www.w3.org/ns/sosa/>
PREFIX oim-jf:  <http://w3id.org/iliad/oim/jellyfish#> 
PREFIX dwc: <http://rs.tdwg.org/dwc/terms/>
PREFIX bif: <bif:>

SELECT DISTINCT ?s ?id_link ?resultTime ?phenomenonTime ?densityOfJF ?sampleSizeValue ?scientificName ?wormsConcept ?datastream_link ?feature_link
WHERE {
    ?s a sosa:Observation ;
    	sosa:resultTime ?resultTime ;
			sosa:phenomenonTime ?phenomenonTime ;
			sosa:hasResult ?result .
    ?result oim-jf:densityOfJF ?densityOfJF ;
			dwc:sampleSizeValue ?sampleSizeValue ;
			dwc:scientificName ?scientificName ;
			oim-jf:wormsConcept ?wormsConcept .
    FILTER (strEnds(STR(?s), CONCAT('/',?_id)))	
    BIND (URI(CONCAT('https://w3id.org/iliad/api/v1.0/Observations(', ?_id,')')) AS ?id_link)
    BIND (URI(CONCAT('https://w3id.org/iliad/api/v1.0/Observations(', ?_id,')/Datastream')) AS ?datastream_link)
    BIND (URI(CONCAT('https://w3id.org/iliad/api/v1.0/Observations(', ?_id,')/FeautureOfInterest')) AS ?feature_link)
}
